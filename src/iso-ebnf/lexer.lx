#
# Copyright 2014-2017 Katherine Flavel
#
# See LICENCE for the full copyright terms.
#

# 7.6: Gap-separator
/[ \t\r\n\v\f]+/;

# Table 4: Invalid sequences of characters
# TODO: update for strings instead
# TODO: permit -> $error for lx
# '(*)' -> $error;
# '(:)' -> $error;
# '(/)' -> $error;

'(*' .. '*)';

"'" .. "'" -> $literal {
	/[^']/ -> $char;
}

'"' .. '"' -> $literal {
	/[^"]/ -> $char;
}

# Table 1: Representation of terminal-charaters
'|' -> $alt;
';' -> $sep;
'[' -> $startopt;  ']' -> $endopt;
'{' -> $startstar; '}' -> $endstar;

# Table 2: Alternative representation of terminal-characters
'/' -> $alt;
'!' -> $alt;
'.' -> $sep;
'(:' -> $startstar; ':)' -> $endstar; # TODO: rename to -rep
'(/' -> $startopt;  '/)' -> $endopt;

# tokens shared by both representations
',' -> $cat;
'=' -> $equals;
'(' -> $startgroup; ')' -> $endgroup;
'-' -> $except;
'*' -> $star;
# XXX: '?' -> $opt;

# spaces are permitted within EBNF names
# XXX: I am unsure that this is correct. EBNF is far too complicated.
# XXX: The ISO EBNF grammar seems to be ambigious with terminal character
# containing hyphen, and I don't see how to distinguish that from an "except"
# operator hyphen. So I'm taking the intuitively reasonable approach of not
# permitting hyphens at the start and end of gap free symbols. This is my
# own definition for $ident, rather than repeating the grammar from the spec.
# TODO: when lx permits, we could remove rtrim() and write this using group
#       capture to consume whitespace here, but disregard it.
word = /[a-z][a-z0-9_]*(-[a-z0-9_]+)*/i;
word (/[\t\r\n\v\f ]*/ word)* /[\t\r\n\v\f ]*/ -> $ident;

/[0-9]+/ -> $count;

# TODO:
'?' .. '?';

